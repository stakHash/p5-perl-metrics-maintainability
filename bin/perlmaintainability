#!/usr/bin/env perl
use strict;
use warnings;
use utf8;
use feature qw/say/;
binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';

use Getopt::Long;
use List::AllUtils qw/any nsort_by rev_nsort_by/;
use Perl::Metrics::Maintainability;


exit main() unless caller;

sub main {
    my ($options, @paths) = parse_options(@ARGV);

    my $defined_sort_options = +{
        mi      => 'asc',
        loc     => 'desc',
        cc      => 'desc',
        volume  => 'desc',
    };

    my @sort_keys = keys %$defined_sort_options;
    $options->{sort} //= 'mi';

    unless (any { $options->{sort} eq $_ } @sort_keys) {
        #TODO: display usage
        my $msg = 'undefined sort type (must be ' . join(', ', @sort_keys) . ')';
        die $msg;
    }

    my $analyzer = Perl::Metrics::Maintainability->new(paths => \@paths);
    my $results = $analyzer->analyze();

    return 1 unless @$results;

    output($results, $options->{sort}, $defined_sort_options);
    return 0;
}

sub parse_options {
    my (@args) = @_;

    #TODO: display usage
    return undef unless @args;

    my $options = +{};

    unless (Getopt::Long::Parser->new()->getoptionsfromarray(
        \@args,
        $options,
        'sort:s',
    )) {
        #TODO: display usage
        return undef;
    }

    return ($options, @args);

}

sub output {
    my ($results, $sort_type, $defined_sort_options) = @_;
    say "MI\tLoC\tmax_cc\tvolume\t\tpath";
    say '--------------------------------------------------------------------------------';

    my @sorted = $defined_sort_options->{$sort_type} eq 'desc'
        ? rev_nsort_by { $_->{$sort_type} } @$results
        : nsort_by { $_->{$sort_type} } @$results;

    for my $result (@sorted) {
        my ($mi, $loc, $max_cc, $volume, $path) = @$result{qw/mi loc cc volume path/};
        say sprintf("%0.2f\t%d\t%d\t%0.2f\t\t%s", $mi, $loc, $max_cc, $volume, $path);
    }
}
